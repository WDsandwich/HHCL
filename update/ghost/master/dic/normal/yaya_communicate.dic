//******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
//******************************************************************************


//******************************************************************************
//こちらから話しかける
//******************************************************************************

//この関数は現状メニューから呼ばれます
StartCommunicate
{
	//以下の関数は、隣に立っているゴーストを探して、
	//TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
	//yaya_tmpl_utilにあります。
	AYATEMPLATE.ExecuteTalkToTalk
}

//エミリに話しかける
TalkTo_Emily{
	"\0\s[0]あ、エミリさん。\1\s[10]テディも元気そうだな。"
}


//その他の人に話しかける
TalkToOther{
	"\0\s[0]こんにちは、%(res_reference0)さん。\1\s[10]元気でやってるか。"
}

TalkToNobody{
	"\0\s[0]だれもいないよ。\1\s[10]静かなもんだ。"
}


//******************************************************************************
//話しかけられたので返事する
//******************************************************************************

//------------------------------------------------------------------------------
//OnCommunicateイベント
//------------------------------------------------------------------------------
OnCommunicate
{
	if reference[0] == "user" || reference[0] == "User"
	{
		//---- ユーザーからの話しかけ
		ReplyToUser
	}
	else
	{
		//---- ゴーストからの話しかけ
		//以下の関数は、ReplyTo_??? 関数を呼び出すものです。
		//yaya_tmpl_utilにあります。
		AYATEMPLATE.ExecuteReplyToTalk(reference[0])
	}
}

//------------------------------------------------------------------------------
//ユーザーからの話しかけ
//------------------------------------------------------------------------------
ReplyToUser
{
	if "名前" _in_ reference[1] && "覚えて" _in_ reference[1]
	{
		//---- ユーザー名入力開始
		"\0\s[0]いいよ、なんていう名前なの、%(usernameHC)？\e"
		teachusername = 1
	}
	else
	{
		//---- ユーザー名入力開始以外
		if teachusername == 1
		{
			//---- 直前が「名前覚えて」だったので名前を覚える
			username = reference[1]
			"\0\s[0]%(usernameHC)、ね。わかりました。\e"
			teachusername = 0
		}
		else
		{
			//---- 名前覚え以外のさまざまな会話への返事
			TalkToUser
		}
	}
}

//------------------------------------------------------------------------------
//ユーザーへの返事（名前覚え以外）
//------------------------------------------------------------------------------
TalkToUser
{
	if chatlog==1 && "真的" _in_ reference[1]
	{
		"\0…\w3…\w4…\w5\s[8]…\w4唔，\w5請您認真工作。"
		--
		chatlog=2
	}
	elseif chatlog==2 && "不喜歡" _in_ reference[1]
	{
		"\0…\w3…\w4…\w5我\s[7]…\w4…\w4我也喜歡\w3%(usernameHC)\w3…\w4…"
		--
		ERASEVAR('chatlog')
	}
	elseif chatlog==4 && reference[1]==0
	{
		"\1\s[11]哼，\w4原來是在捉弄我嗎？"
	}
	elseif chatlog==4 && GETTYPE(reference[1])==1 && reference[1]>1000
	{
		"\1\s[16]咦，\w4這麼多……\w6 沒關係嗎？"
		--
		chatlog=5
	}
	elseif chatlog==4 && GETTYPE(reference[1])==1 && reference[1]>0
	{
		"\1\s[12]哇－－\w4%(usernameHL)最好了！！"
		--
		discoin-=reference[1]
		intimacy[1]+=reference[1]/10
		ERASEVAR('chatlog')
	}
	elseif chatlog==5 && ("沒關係" _in_ reference[1] || "沒關系" _in_ reference[1] || "無要緊" _in_ reference[1])
	{
		"\1\s[13]…\w2…\w3唔…\w2…\w3嗯…\w2…\w6 \s[12]那我就不客氣地收下囉！"
		--
		discoin-=1000
		intimacy[1]+=10
		ERASEVAR('chatlog')
	}
	elseif chatlog==5 && ("不要" _in_ reference[1] || "算了" _in_ reference[1])
	{
		"\1\s[13]真是的，\w5我也不需要\w3那麼多啦\w2…\w2…"
		--
		ERASEVAR('chatlog')
	}
	else
	{
		case MenuChoice
		{ when 0
		  {
			if "愛" _in_ reference[1]
			{
				"\0\s[4]咦\w2…\w3…\w4…\w5…\w6 \s[7]突然說這種話……\w5"
				--
				chatlog=1
			}
			elseif "天氣" _in_ reference[1]
			{
				"\0抱歉，\w5\s[1]我一直待在管理局內，\w7無法得知室外的情況。"
			}
			elseif "海拉" _in_ reference[1]
			{
				"\0海拉，\w5%(usernameHC)在叫妳。\w9\w5\c\b[-1]"
				--
				MenuChoice=1
				OpenMenu
			}
			else
				"\0\s[1]抱歉，\w4我不理解……"
		  }
		  when 1
		  {
			if "赫卡蒂" _in_ reference[1]
			{
				"\1赫卡蒂，\w5%(usernameHC)在叫妳\w2－\w2－\w9\w5\c\b[-1]"
				--
				MenuChoice=0
				OpenMenu
			}
			elseif "零用錢" _in_ reference[1] || "零花錢" _in_ reference[1]
			{
				"\1\s[16]哇－－\w6 真的嗎真的嗎？\w9\n\s[12]嘻嘻，\w5%(you)要給我多少呀？\n/
				%(STdiscoin)"
				--
				chatlog=4
			}
			else
				"\1\s[13]聽不懂……"
		  }
		}
	}
}

//------------------------------------------------------------------------------
//ゴーストからの話しかけ
//------------------------------------------------------------------------------

//エミリから話しかけられた
ReplyTo_Emily{
	res_reference0=reference[0]	//さらに相手に話しかける
	"\0\s[0]なんですか、エミリさん。\1\s[10]芋ばっかり食べてちゃだめだぞ。\e"
}

ReplyTo_Hand{
	if "ほっく" _in_ reference[1] {
		res_reference0=reference[0]	//さらに相手に話しかける
		"\0\s[0]ほっくなんて無いよ。\1\s[10]ジッパーはあるけどな。\e"
	}else{
		"\1\s[10]うるさい手だな。\0\s[0]なんだろうねあれ。\e"
	}
}


//その他の人から話しかけられた
ReplyToOther{
	if "こんにちは" _in_ reference[1] || "こんばんは" _in_ reference[1]
	{
		res_reference0=reference[0]	//さらに相手に話しかける
		"\0\s[0]どうも、%(res_reference0)。\1\s[10]うむ。%(reference[1])\e"
	}
	else
	{
		"\0\s[0]はい？\e"
	}
}

