OnHotkeySetting
{
	"\p[4]\s[-1]\b[6]%(click)\![quicksession,true]\n[50]\f[height,15]%Subtitle('自訂快捷鍵')\n[150]"
	--
	_name="關閉程式,重整位置,顯示局長,禁閉者檔案,倉庫,互動,採購部,更衣室,研磨坊,戰鬥,溫泉,派遣,圖鑑,鍛造室,檔案室"
	_txt=""
	for _i=0 ; _i<ARRAYSIZE(_name) ; _i++
	{
		_txt+="\_l[10]\f[color,200,0,0]\f[height,20]└\f[default]\f[height,15] \__q[OnHotkeySetting.Change,%(_i),%(_name[_i])]%(_name[_i])\n[1]\f[align,right] [  \f[bold,1]\f[color,"
		if OnHotkeySetting.Check(self_hotkey[_i]) >1 ; _txt+="#FF2D2D"
		else ; _txt+="#FFDC35"
		_txt+="]%(self_hotkey[_i])\f[color,default]\f[bold,0]  ] \__q \__q[OnHotkeySetting.Delelt,%(_i)]%QTYimg('x')\__q    \n[130]\f[align,left]"
	}
	_txt
	--
	"\_l[-,480]"
	--
	MuenBottomOption2('Office')
}
OnHotkeySetting.Check
{
	_ob=_argv[0]
	_result=0
	for _i=0 ; _i<ARRAYSIZE(self_hotkey) ; _i++
	{
		if self_hotkey[_i]==_ob ; _result++
	}
	_result
}
OnHotkeySetting.Change
{
	HotkeyMode=1
	hotkey_ob=reference0
	--
	OnHotkeySetting
	--
	SureWindow('OnHotkeySetting')
	--
	"\f[align,center]請按下欲設定的快捷鍵\n[130]（\f[color,#FFDC35]\f[bold,1]%(reference[1])\f[default]）"
}
OnHotkeySetting.End
{
	HotkeyMode=0
	self_hotkey[hotkey_ob]=reference0
	--
	OnHotkeySetting
}
OnHotkeySetting.Delelt
{
	self_hotkey[reference0]=""
	--
	OnHotkeySetting
}
BalloonTooltip_OnHotkeySetting.Delelt{"清空"}



//快捷鍵列表
OnKeyPress
{
	_key=reference0
	if EventArgv>0 ; MouseInEvent
	elseif HotkeyMode==1 ; { "\![raise,OnHotkeySetting.End,%(reference0)]" }
	elseif ill_mode==2			//圖鑑切換
	{	case _key
		{	when "left"  ; "\![raise,OnIllustrated_Enemy_Info.Changepage,A]"
			when "right" ; "\![raise,OnIllustrated_Enemy_Info.Changepage,B]"
			when "up"	 ; "\![raise,OnIllustrated_Enemy_Info.ChangePic,-1]"
			when "down"  ; "\![raise,OnIllustrated_Enemy_Info.ChangePic,1]"
		}
	}
	elseif _key==self_hotkey[0] {HotKey_CloseSSP}	//關閉程式
	elseif _key==self_hotkey[1] {ResetSpot}	//重整位置
	elseif _key==self_hotkey[2] {cp(0)}	//顯示局長
	elseif _key==self_hotkey[3] {SinnerInfo}	//禁閉者檔案
	elseif _key==self_hotkey[4] {OpenBagItem}	//倉庫
	elseif _key==self_hotkey[5] {Menu_Interact}	//互動
	elseif _key==self_hotkey[6]			//採購部
	{	if StoreMode==0 ; Menu_STORE
		else ; OnLeaveStore	}
	elseif _key==self_hotkey[7]	//更衣室
	{	if DressingMode==0 ; GoIntoDressingRoom
		elseif DressingCheck!=0 {DressingCheck}
		else ; OnLeaveDressingRoom
	}
	elseif _key==self_hotkey[8] {Dye.Grind.GoTo}	//研磨坊
	elseif _key==self_hotkey[9] {GoToBattle}	//戰鬥
	elseif _key==self_hotkey[10] {GoToSpa}	//溫泉
	elseif _key==self_hotkey[11] {Dispatch}	//派遣
	elseif _key==self_hotkey[12] {HotKey_Illustrated}	//圖鑑
	elseif _key==self_hotkey[13] {Hotkey_ForgeRoom}	//鍛造室
	elseif _key==self_hotkey[14] {Database}	//檔案室
	//if _key=="f2" ; {BattleMode=0}
	if developermode==1
	{  case reference0 {
		when "g" { if developermode==1 ; "\![reload,ghost]" }
		when "s" { if developermode==1 ; "\![reload,shell]" }
		when "b" { if developermode==1 ; "\![reload,balloon]" }
		
		
		when "q" {
		cum_enemy[14]=ANY(1,2,3,4)
		}
		when "v" {/*
		_txt="\p[4]\s[-1]\b[42]\f[valign,center]\![quicksession,true]\![set,autoscroll,disable]"
		_l=(1,5,100,104,270,274,350,354)
		_type=("強化道具","體力回復","特殊禮物","普通禮物")
		_pro=(10,10,20,60)
		for _t=0 ; _t<ARRAYSIZE(_type) ; _t++
		{
			_a=_t*2 ; _b=_a+1
			_txt+="\f[height,15]\_l[1,"
			_txt+=_l[_a]
			_txt+="]%Bimg('\store\square7')\_l[20,"
			_txt+=_l[_b]
			_txt+="]%(_type[_t])  \n[1]\f[align,right]共%(_pro[_t])%   \f[height,12]\n[70]"
			_argv=""
			case _t
			{	when 0 ; _argv=enhancement_name
				when 1 ; _argv=consumable_name
				when 2 ; _argv=gift_sp_name
				when 3 ; _argv=gift_name
			}
			for _i=0 ; _i<ARRAYSIZE(_argv) ; _i++
			{
				_txt+="\n[130]\f[align,left]\_l[30]%(_argv[_i])"
			}
		}
		_txt+="\n"
		_txt
		*/}
		when "n" {
		holiday_celebrated_store[3]=""
		}
		when "p" {
		"\![open,dialog,color,--id=Onjrioj]"
		}
		when "t" {
		dye_ingredient+=100
		}
		when "k" {
		OnTutorial
		}
		when "." {
		wearing_cloth_HL
		--
		"\n"
		--
		wearing_cloth_HL_last
		}
		when "," {
		illustrated_list=enemy_ingredient_1
			illustrated_title=dye_ingredient_name[1]
			Illustrated_Type_Enemy
		}
		when "j" {
		
		"\![raise,OnTutorial.Battle,41]"
		}
		when "w" {
		"\p[5]\s[520010]%SFX('sfx/Rinwonarasu')\i[680001]"
		}
		when "e" {
		"\![raise,OnTutorial.Battle,20]"
		}
		when "i" {
		
		}
		/*when "l" {
			if ISFUNC('OpenMenu') {
				OpenMenu
			}
			else {
				'\![raise,OnMouseDoubleClick,0,0,0,0,__SYSTEM_KEYDOWN_COL,0,mouse]'
			}
		}*/
	  }
	}
}
ResetSpot
{
	"\0\![move,--X=0,--Y=-680,--time=10,--base-offset=left.bottom]\1\![move,--X=-300,--Y=-650,--time=10,--base-offset=right.bottom]\p[2]%cp(0)\![move,--X=750,--Y=-200,--time=10,--base-offset=left.bottom]\p[3]\![move,--X=900,--Y=-250,--time=10,--base-offset=left.bottom]\p[4]\s[-1]\![move,--X=750,--Y=500,--time=10,--base=screen]\p[5]\s[520010]\![set,balloonalign,top]\![move,--X=500,--Y=440,--time=10,--base-offset=left.top]（戰鬥對白）\p[6]\s[-1]（戰鬥訊息窗）\![move,--X=750,--Y=500,--time=10,--base=screen]\p[7]\s[-1]\![set,balloonalign,right]\![move,--X=750,--Y=500,--time=10,--base=screen]（提示訊息對話框）"
}
HotKey_CloseSSP
{
	"\p[5]\b[50]\s[-1]\![quicksession,true]\_l[236,-25]\__q[CENCEL]%QTYimg('x')\__q\n[230]/
	\f[align,center]確定關閉程式嗎？\n[150]/
	\f[height,12]%DRimg('mark1')每次按下快捷鍵都提醒\n[300]/
	\_l[66,70]\__q[OnClose]\_b[img\O.png,inline]\__q   \_l[153]\__q[CENCEL]\_b[img\X.png,inline]\__q\e"
}


